# load completion scripts
fpath=(~/.zsh/completions $fpath)

# load custom executable functions
 for function in ~/.zsh/functions/*; do
   source $function
 done

# extra files in ~/.zsh/configs/pre , ~/.zsh/configs , and ~/.zsh/configs/post
# these are loaded first, second, and third, respectively.
_load_settings() {
  _dir="$1"
  if [ -d "$_dir" ]; then
    if [ -d "$_dir/pre" ]; then
      for config in "$_dir"/pre/**/*(N-.); do
        source $config
      done
    fi

    for config in "$_dir"/**/*(N-.); do
      case "$config" in
        "$_dir"/(pre|post)/*|*.zwc)
          :
          ;;
        *)
          source $config
          ;;
      esac
    done

    if [ -d "$_dir/post" ]; then
      for config in "$_dir"/post/**/*(N-.); do
        source $config
      done
    fi
  fi
}
_load_settings "$HOME/.zsh/configs"

# Local config
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

# aliases
[[ -f ~/.zsh/aliases.sh ]] && source ~/.zsh/aliases.sh

bindkey -v # vim keys

for bind in ~/.zsh/binds/*; do
    source $bind
done

[[ -f ~/.cache/wal/colors.sh ]] && source ~/.cache/wal/colors.sh
